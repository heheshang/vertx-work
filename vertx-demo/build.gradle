plugins {
    id 'java'
}

version '3.5.0'

ext {
    vertxVersion = "3.5.0"
}

jar {
    // by default fat jar
    archiveName = 'vertx-blueprint-todo-backend-fat.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Launcher'
        attributes 'Main-Verticle': 'com.vertx.demo.verticle.TodoVerticle'
    }
}

repositories {
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    mavenCentral()
}

// compileOnly requires Gradle 2.12+
task annotationProcessing(type: JavaCompile, group: 'build') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.compileOnly
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
            "-proc:only",
            "-processor", "io.vertx.codegen.CodeGenProcessor",
            "-Acodegen.output=${destinationDir.absolutePath}"
    ]
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/generated'
        }
    }
}

compileJava {
    targetCompatibility = 1.8
    sourceCompatibility = 1.8

    dependsOn annotationProcessing
}

dependencies {
    compile("io.vertx:vertx-core:${vertxVersion}")
    compile("io.vertx:vertx-rx-java2:${vertxVersion}")
    compile("io.vertx:vertx-web:${vertxVersion}")
    compile("io.vertx:vertx-jdbc-client:${vertxVersion}")
    compile("io.vertx:vertx-redis-client:${vertxVersion}")
    compileOnly("io.vertx:vertx-codegen:${vertxVersion}")
    compile 'mysql:mysql-connector-java:6.0.5'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    compile group: 'org.slf4j', name:'slf4j-api', version: '1.7.2'
    compile group: 'ch.qos.logback', name:'logback-classic', version: '1.0.9'
    compile group: 'ch.qos.logback', name:'logback-core', version: '1.0.9'
    compileOnly 'org.projectlombok:lombok:1.18.4'
    testCompile("io.vertx:vertx-unit:${vertxVersion}")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}
